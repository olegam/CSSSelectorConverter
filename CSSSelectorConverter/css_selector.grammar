/*
A CSS Selector to XPath Selector Grammar
*/
@symbols = '=' '~=';

@start
@before{
  PKTokenizer *t = self.tokenizer;
 [t.symbolState add:@"~="];
}                       = selectorsGroup;                        
selectorsGroup          = selector ( ','! selector )* {
  PUSH_SELECTORS_GROUP();
};
selector                = simpleSelectorSequence ((combinator {
  PUSH_COMBINATOR();
} simpleSelectorSequence) | simpleSelectorSequence)* {
  PUSH_SELECTORS();
};

simpleSelectorSequence  = (universalSelector | typeSelector)? (classSelector | idSelector | attributeSelector)* {
  PUSH_SELECTOR_SEQUENCE();
};
attributeSelector       = '['! Word (Empty | ((equal | includes) QuotedString )) ']'! {
  PUSH_ATTRIBUTE();
};
classSelector           = '.'! Word {
  PUSH_CSS_CLASS(POP_STR());
};
idSelector              = '#'! Word {
  PUSH_CSS_ID(POP_STR());
};
typeSelector            = Word {
  PUSH_CSS_TYPE(POP_STR());
};
universalSelector       = '*'! {
  PUSH_CSS_UNIVERSAL();
};

combinator = '>' | '+' ;
equal                   = "=";
includes                = "~=";
