CSSSelectorGroup                ::= firstSelector@<CSSSelectors> otherSelectors@(',' 'Whitespace'* <CSSSelectors>)*;
CSSSelectors                    ::= firstSequence@<CSSSelectorSequence> otherSequences@(<CSSCombinator> <CSSSelectorSequence>)*;
CSSSelectorSequence             ::= (universal@<CSSUniversalSelector> | type@<CSSTypeSelector>) selectorsWithType@(<CSSClassSelector> | <CSSIDSelector> | <CSSSelectorAttribute> | <CSSPseudoClass>)* | selectorsWithoutType@(<CSSClassSelector> | <CSSIDSelector> | <CSSSelectorAttribute> | <CSSPseudoClass>)+;
CSSSelectorAttribute            ::= '[' 'Whitespace'* attrName@'Identifier' 'Whitespace'* (op@<CSSSelectorAttributeOperator> 'Whitespace'* attrValue@<QuotedString>)? ']';
CSSSelectorAttributeOperator    ::= <Equal> | <Includes> | <Dashmatch>;
CSSPseudoClass                  ::= ':' ':'? (className@'Identifier');
CSSCombinator                   ::= <Greater>  'Whitespace'* | <Plus>  'Whitespace'* | <Tilde> 'Whitespace'*| 'Whitespace'+;
CSSClassSelector                ::= '.' className@'Identifier';
CSSIDSelector                   ::= '#' idName@'Identifier';
CSSTypeSelector                 ::= typeName@'Identifier';
CSSUniversalSelector            ::= '*';
CSSPseudoExpression             ::= (( '+' | '-' | 'Number' 'Identifier' | 'Number' | 'Identifier' ) 'Whitespace'* )+;

QuotedString                    ::= 'DoubleQuoted' | 'SingleQuoted';
Dashmatch                       ::= '|=';
Includes                        ::= '~=';
Equal                           ::= '=';

Plus                            ::= '+';
Greater                         ::= '>';
Tilde                           ::= '~';